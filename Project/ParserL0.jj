PARSER_BEGIN(Parser)

import java.util.*;

public class Parser {
}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\r"
| "\n"
}

TOKEN :
{

  < NIL : "nil" >
  |
  < CONS: "::" >
  |
  < LAZYCONS: ":?">
  |
  < MATCH: "match" >
  |
  < BAR : "|" >
  |
  < SARROW: "->" >
  |
  < LET : "let" >
  |
  < TYPE : "type" >
  |
  < TRUE: "true" >
  |
  < FALSE: "false" >
  |
  < PLUS : "+" >
  |
  < MINUS : "-">
  |
  < STAR : "*">
  |
  < DIV : "/">
  |
  < LPAR : "(" >
  |
  < RPAR : ")" >
  |
  < LBRA : "{" >
  |
  < RBRA : "}" >
  |
  < EQUAL : "=" >
  |
  < DOT : "." >
  |
  < COLON : ":" >
  |
  < SEMIC : ";" >
  |
  < TERM : ";;" >
  |
  < COMMA : "," >
  |
  < AND : "&&" >
  |
  < OR : "||" >
  |
  < EQ : "==" >
  |  
  < GT : ">" >
  |
  < LT : "<" >
  |
  < GTEQ : ">=" >
  |
  < LTEQ : "<=" >
  |
  < DIF : "~=" >
  |
  < NOT : "~" >
  |
  < ASSIGN : ":=" >
  |
  < BOX : "box" >
  |
  < IF : "if" >
  |
  < ELSE : "else" >
  |
  < WHILE : "while" >
  |
  < PRINT : "print" >
  |
  < PRINTLN : "println" >
  |
  < FN : "fn" >
  |
  < ARROW : "=>" >
  |
  <INT  : "int">
  |
  <UNIT  : "()">
  |
  <BOOL  : "bool">
  |
  <STRING  : "string">  
  |
  <LIST  : "list">  
  |
  <REF  : "ref">  
  |
  <UNION  : "union">  
  |
  <STRUCT  : "struct">  
  |
  < String: "\"" ( (~["\"","\\"]) | ("\\" ( ["n","t","b","r","f","\\","\""] ) ) )* "\"" >
  |
  < Label: "#" (["a"-"z","A"-"Z"])+ >
  |
  < Id: ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","0"-"9"] )* >
  |
  < Num: (["0"-"9"]) + >
}

ASTNode Start():
{ ASTNode t; }
{
    <EOF> {return null;}
  | t = Let() <TERM> { return t; }
}

ASTNode Let() :
{
  Token n;
  ASTNode e1, body;
  ASTType t1;
  List<Bind> decls = new ArrayList<Bind>();
  HashMap<String, ASTType> types = new HashMap<String, ASTType>();
}
{
    (
      (
        (
          <LET> n=<Id> <EQUAL> e1 = BA() <SEMIC>
          { decls.add(new Bind(n.image, e1)); }
        )
      | (
          <TYPE> n=<Id> <EQUAL> t1 = Type() <SEMIC>
          { types.put(n.image, t1); }
        )
      )*
      body = Seq()
      { return new ASTLetAndType(decls, types, body); }
    )
}


ASTNode Seq() :
{Token op;
  ASTNode t1, t2;}
{
     t1 = SeqExp() ( ( op=<SEMIC> ) t2 = SeqExp() 
 		 {
      t1 = new ASTSeq(t1, t2);
     } 
		)*
     { return t1; }  
}

ASTNode SeqExp() :
{Token op;
  ASTNode t1, t2;}
{
     t1 = BA() ( ( op=<ASSIGN> ) t2 = BA() 
 		 {
      t1 = new ASTSeqExp(t1, t2);
     } 
		)*
     { return t1; }  
}

ASTNode BA() :
{Token op;
  ASTNode t1, t2;}
{
     t1 = BM() ( ( op=<OR> ) t2 = BM() 
 		 {
      t1 = new ASTBExp(t1, op.image, t2);
		 } 
		)*
     { return t1; } 
}

ASTNode BM() :
{Token op;
  ASTNode t1, t2;}
{
     t1 = Rel() ( ( op=<AND> ) t2 = Rel() 
 		 {
      t1 = new ASTBExp(t1, op.image, t2);
 		 } 
		)*
     { return t1; } 
}


ASTNode Rel() :
{Token op;
  ASTNode t1, t2;}
{
     t1 = Exp() ( ( op=<EQ> | op=<GT> | op=<LT> | op=<GTEQ> | op=<LTEQ> | op=<DIF>) t2 = Exp() 
     	  	  {
              t1 = new ASTComp(t1, op.image, t2);
		   } 
		)?
     { return t1; } 
}

ASTNode Exp() :
{
    ASTNode t1, t2;
}
{
    t1 = Term() (
        <PLUS> t2 = Term() { t1 = new ASTPlus(t1, t2); }
      | <MINUS> t2 = Term() { t1 = new ASTSub(t1, t2); }
    )*
    { return t1; }
}

ASTNode RecordTerm() :
{
    Token label;
    ASTNode value;
    HashMap<String, ASTNode> fields = new HashMap<String, ASTNode>();
}
{
    <LBRA>
    label = <Label> <EQUAL> value = Term() { fields.put(label.image, value); }
    ( <COMMA> label = <Label> <EQUAL> value = Term() { fields.put(label.image, value); } )*
    <RBRA>
    { return new ASTStruct(fields); }
}

ASTNode Term() :
{
    ASTNode t1, t2;
    Token op;
}
{
    t1 = Fact() (
        <CONS> t2 = Term() { t1 = new ASTCons(t1, t2, false); }
      | <LAZYCONS> t2 = Term() { t1 = new ASTCons(t1, t2, true); }
      | (
        op=<STAR> t2 = Fact() { t1 = new ASTMult(t1,t2); }
      | op=<DIV> t2 = Fact() { t1 = new ASTDiv(t1,t2); }
      | op = <LPAR> t2 = Exp () <RPAR> { t1 = new ASTApp(t1, t2); }
    )*
    ) { return t1; }
}


ASTNode Fact() :
{ Token n, n2, m; 
  ASTNode t, e1, e2;
  List<Bind> decls;
  ASTNode  body, alt;
  ASTType at;
}
{
   (
      n=<Num> { t = new ASTInt(Integer.parseInt(n.image)); } 
    | n=<TRUE> { t = new ASTTrue(); }
    | n=<FALSE> {  t = new ASTFalse();}
    | LOOKAHEAD(2) n=<Label> <UNIT> { t = new ASTUnion(n.image, new ASTUnit()); }
    | n=<Label> <LPAR> e1 = Term() <RPAR> { t = new ASTUnion(n.image, e1); }
    | n=<Id> { t = new ASTId(n.image); } 
    | <NIL> { t = new ASTNil(); }
    | <BOX> t=Fact() {  t = new ASTBox(t); }
    | <STAR> t=Fact() { t = new ASTStar(t); }
    | n=<String> { t = new ASTString(n.image.substring(1, n.image.length()-1)); }
    | <MINUS> t=Fact() { t = new ASTNeg(t); }
    | <NOT> t=Fact() {t = new ASTNot(t);  }
    | <IF> t=BA() <LBRA> e1=Let() <RBRA> <ELSE> <LBRA> e2=Let() <RBRA> { t = new ASTIf(t, e1, e2); }
    | <WHILE> t=BA() <LBRA> body=Let() <RBRA> { t = new ASTWhile(t, body); }
    | <FN> n=<Id> <COLON> at = Type(){t = new ASTFun(n.image, null); e1 = t; }
             	  (<COMMA> n = <Id> <COLON> at = Type() {e2 = new ASTFun(n.image, null);
              ((ASTFun)e1).setBody(e2); e1 = e2; })*
          <ARROW> <LBRA> e2 = Let() <RBRA> { ((ASTFun)e1).setBody(e2); }
    | LOOKAHEAD(4) <MATCH> t=Let() <LBRA> <NIL> <SARROW> e1=Let()
      <BAR> n=<Id> <CONS> m=<Id> <SARROW> e2=Let() <RBRA> 
        { t = new ASTMatch(t, e1, n.image, m.image, e2); }
    | LOOKAHEAD(4) <MATCH> t = Let() <LBRA>
      n = <Label> <LPAR> m = <Id> <RPAR> <SARROW> e1=Let()
      {
        ArrayList<String> labels = new ArrayList<String>();
        ArrayList<String> vars = new ArrayList<String>();
        ArrayList<ASTNode> bodies = new ArrayList<ASTNode>();
        labels.add(n.image);
        vars.add(m.image);
        bodies.add(e1);
      }
      (
        <BAR> n = <Label> <LPAR> m = <Id> <RPAR> <SARROW> e2 = Let()
         {
          labels.add(n.image);
          vars.add(m.image);
          bodies.add(e2);
         }
      )*
      <RBRA>
      { t = new ASTMatchUnion(t, labels, vars, bodies); }
    | <PRINT> t=Fact() { t = new ASTPrint(t);  }
    | <PRINTLN> t=Fact() { t = new ASTPrintln(t);  }
    | <LPAR> t=Let() <RPAR> 
    | t = RecordTerm()
    )
    (
      LOOKAHEAD(2)
      <DOT> n = <Id> { t = new ASTFieldSelect(t, n.image); }
    )*

   { return t; }
}


ASTType Type() : 
{
  ASTType t1, t2;
}
{        
    t1 = TypeF() (<SARROW> t2 = Type() { t1 = new ASTTArrow(t1,t2);} )?
    { return t1; }
}

TypeBindList LabelList() :
{
  ASTType t;
  HashMap<String,ASTType> ll;
  Token n;
}
{   { ll = new HashMap<String,ASTType>() ; }
    ( n=<Label>  <COLON> t = Type() { ll.put(n.image,t); } 
              (<COMMA> n=<Label>  <COLON> t = Type() { ll.put(n.image,t);} )*
    )?
    { return new TypeBindList(ll); }
}

ASTType TypeF() : 
{
  ASTType t;
  TypeBindList ll;
  Token n;
}
{        
    ( 
          <INT> { t = new ASTTInt(); } 
        | n=<Id> { t = new ASTTId(n.image); } 
        | <BOOL> { t = new ASTTBool(); } 
        | <UNIT> { t = new ASTTUnit(); } 
        | <STRING> { t = new ASTTString(); } 
        | <REF> <LT> t=Type() <GT> { t = new ASTTRef(t); }
        | <LIST> <LT> t=Type() <GT> { t = new ASTTList(t); }
        | <STRUCT> <LBRA> ll = LabelList() <RBRA> { t = new ASTTStruct(ll);}
        | <UNION> <LBRA> ll = LabelList() <RBRA> { t = new ASTTUnion(ll);}
    )
    { return t; }
}